buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.wrapKeys = { map ->
    def newMap = [:]
    map.each { k, v ->
        def newk = '${'+k+'}'
        newMap[newk] = v
    }
    return newMap
}

def modprops = new Properties()
file('mod.properties').withInputStream {
    stream -> modprops.load(stream)
}

version = modprops.__MOD_VER__
group= "com.github.rintaun.${modprops.__MOD_ID__}"
archivesBaseName = "${modprops.__MOD_ID__}-${modprops.__MOD_MCVER__}"

logger.quiet("Building ${modprops.__MOD_NAME__} v${version}")
logger.quiet("  (${archivesBaseName}-${version}.jar)")
logger.quiet("");

minecraft {
    version = modprops.__MOD_MCVER__ + '-' + modprops.__MOD_FORGEVER__
    runDir = "eclipse"

    mappings = "snapshot_20141130"
}

dependencies {
}

sourceMainJava
{
    replace(wrapKeys(modprops))
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand modprops
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
